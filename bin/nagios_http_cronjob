#!/usr/bin/perl
#
# Helper script to setup a cron job running as nagios with the given command.
#
# Ideally we should be able to do this via some kind of user cron.d facility,
# but such a thing doesn't seem to exist, at least with linux vixie-cron.
#

use strict;
use File::Basename;
use Getopt::Long;
use IO::File;
use Nagios::HTTP::Util;

my $max_freq = 12 * 60;

sub usage { die "usage: " . basename($0) . " [--freq <minutes>] [-u <url>] -n <name> -C <cmd>\n"; }

my $url = 'http://nagios/nagios-http';
my ($cmd, $freq, $name, $stdout, $verbose);
usage
unless GetOptions(
  'cmd|C=s'         => \$cmd,
  'name|n=s'        => \$name,
  'freq|f=i'        => \$freq,
  'stdout'          => \$stdout,
  'url|u=s'         => \$url,
  'verbose|v+'      => \$verbose,
);
usage unless $cmd;

# Generate hash from current settings
my $hash = Nagios::HTTP::Util::gen_hash( cmd => $cmd, freq => $freq );

# Map frequency to cron style $minute $hour settings
$freq ||= 5;
my $cron = '';
my $min = '*';
my $hour = '*';
if ($freq >= 60) {
  die "Error: frequency '$freq' is too large - max is $max_freq\n" 
    if $freq > $max_freq;
  die "Error: frequency must be a factor of 60 if greater than 60\n"
    if $freq % 60;
  $min = 45;
  $hour = '*/' . $freq / 60 if $freq > 60;
}
elsif ($freq > 30) {
  die "Error: frequency '$freq' is too large - max is 30 (or a multiple of 60)\n";
}
else {
  $min = "*/$freq";
}

# Output
$cmd =~ s/%/\\%/g;
my $croncmd = "$cmd | /usr/lib/nagios/plugins/nagios_http_result -n $name -h $hash --url $url";
my $cronjob = "$min $hour * * * nagios $croncmd\n";
if ($stdout) {
  print $cronjob;
  exit;
}

# Setup cron file
my $cronfile = "/etc/cron.d/nagios-http-$name";
my $cronfh = IO::File->new( $cronfile, 'w' ) 
  or die "Cannot write to '$cronfile': $!\n";
print $cronfh "PATH=/usr/lib/nagios/plugins:/usr/lib64/nagios/plugins:/bin:/usr/bin\n\n$cronjob\n"
  or die "Print to '$cronfile' failed: $!\n";
$cronfh->close;

# Also, try and run the command manually to avoid having to wait for cron
system($croncmd);
#system("$croncmd -U nagios");

