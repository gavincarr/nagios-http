#!/usr/bin/perl
#
# Helper script to setup a cron job running as nagios with the given command.
#
# Ideally we should be able to do this via some kind of user cron.d facility,
# but such a thing doesn't seem to exist, at least with linux vixie-cron.
#

use strict;
use File::Basename;
use Getopt::Long qw(:config no_ignore_case bundling);
use IO::File;

use Nagios::HTTP::Util;

my $max_freq = 12 * 60;
my $url = 'http://nagios/nagios-http';

sub usage { die "usage: " . basename($0) . " [--freq <minutes>] [-u <url>] [-H hostname] -n <name> -C <cmd>\n"; }

my ($cmd, @env, $freq, $hostname, $name, $stdout, $verbose);
usage
unless GetOptions(
  'cmd|C=s'         => \$cmd,
  'env|E=s@'        => \@env,
  'name|n=s'        => \$name,
  'freq|f=i'        => \$freq,
  'hostname|H=s'    => \$hostname,
  'stdout'          => \$stdout,
  'url|u=s'         => \$url,
  'verbose|v+'      => \$verbose,
);
usage unless $cmd;

# Untaint parameters
if ($name =~ m/^([-\w]+)$/) {
  $name = $1;
} else {
  die "Invalid characters in name '$name'";
}
if ($cmd =~ m! ^([-\w\s'",.;:@\#/\\\$=%&?&\*\(\)]+)$ !x) {
  $cmd = $1;
} else {
  die "Invalid characters in cmd '$cmd'";
}
if (@env) {
  my @untainted_env = ();
  for my $env (@env) {
    if ($env =~ m/^([-\w=]+)$/) {
      push @untainted_env, $1;
    } else {
      die "Invalid characters in env '$env'";
    }
  }
  @env = @untainted_env;
}
if ($hostname) {
  if ($hostname =~ m! ^([\w]+)$ !x) {
    $hostname = $1;
  } else {
    die "Invalid characters in hostname '$hostname'";
  }
}
if ($freq) {
  if ($freq =~ m! ^([\d]+)$ !x) {
    $freq = $1;
  } else {
    die "Invalid characters in frequency '$freq'";
  }
}
if ($url) {
  if ($url =~ m! ^([-\w:/.?&;%,+@\#]+)$ !x) {
    $url = $1;
  } else {
    die "Invalid characters in url '$url'";
  }
}

# Generate hash from current settings
my $hash = Nagios::HTTP::Util::gen_hash( 
  cmd => $cmd, freq => $freq, env => \@env, url => \$url, verbose => $verbose,
);

# Map frequency to cron style $minute $hour settings
$freq ||= 5;
my $cron = '';
my $min = '*';
my $hour = '*';
if ($freq >= 60) {
  die "Error: frequency '$freq' is too large - max is $max_freq\n" 
    if $freq > $max_freq;
  die "Error: frequency must be a factor of 60 if greater than 60\n"
    if $freq % 60;
  $min = 45;
  $hour = '*/' . $freq / 60 if $freq > 60;
}
elsif ($freq > 30) {
  die "Error: frequency '$freq' is too large - max is 30 (or a multiple of 60)\n";
}
else {
  $min = "*/$freq";
}

# Output
$cmd =~ s/%/\\%/g;
my $hostname_arg = $hostname ? "-H $hostname" : '';
my $croncmd = "$cmd | /usr/lib/nagios/plugins/nagios_http_result -n $name -h $hash --url $url $hostname_arg";
my $cronjob = "$min $hour * * * nagios $croncmd\n";
if ($stdout) {
  print $cronjob;
  exit;
}

# Setup cron file
umask 0022;
my $cronfile = "/etc/cron.d/nagios-http-$name";
my $cronfh = IO::File->new( $cronfile, 'w' ) 
  or die "Cannot write to '$cronfile': $!\n";
print $cronfh "PATH=/usr/lib/nagios/plugins:/usr/lib64/nagios/plugins:/bin:/usr/bin\n"
  or die "Print to '$cronfile' failed: $!\n";
if (@env) {
  print $cronfh "$_\n" 
    or die "Print to '$cronfile' failed: $!\n"
      foreach @env;
}
print $cronfh "\n$cronjob\n"
  or die "Print to '$cronfile' failed: $!\n";
$cronfh->close;
chown 0, 0, $cronfile;

# Also, try and run the command manually to avoid having to wait for cron
$ENV{PATH} = '/bin:/usr/bin:/usr/lib/nagios/plugins:/usr/lib64/nagios/plugins';
system($croncmd);

