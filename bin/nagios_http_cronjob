#!/usr/bin/perl
#
# Helper script to setup a cron job running as nagios with the given command.
#
# Ideally we should be able to do this via some kind of user cron.d facility,
# but such a thing doesn't seem to exist, at least with linux vixie-cron.
#

use strict;
use File::Basename;
use Getopt::Long;
use IO::File;
use Nagios::HTTP::Util qw(gen_hash);

my $max_freq = 12 * 60;

sub usage { die "usage: " . basename($0) . " [--freq <minutes>] -n <name> -C <cmd>\n"; }

my $freq = 5;
my ($cmd, $name, $stdout, $verbose);
usage
unless GetOptions(
  'cmd|C=s'         => \$cmd,
  'name|n=s'        => \$name,
  'freq|f=i'        => \$freq,
  'stdout'          => \$stdout,
  'verbose|v+'      => \$verbose,
);
usage unless $cmd;

# Map frequency to cron style $minute $hour settings
my $cron = '';
my $min = '*';
my $hour = '*';
if ($freq >= 60) {
  die "Error: frequency '$freq' is too large - max is $max_freq\n" 
    if $freq > $max_freq;
  die "Error: frequency must be a factor of 60 if greater than 60\n"
    if $freq % 60;
  $min = 45;
  $hour = '*/' . $freq / 60 if $freq > 60;
}
elsif ($freq > 30) {
  die "Error: frequency '$freq' is too large - max is 30 (or a multiple of 60)\n";
}
else {
  $min = "*/$freq";
}

# Generate hash from current settings
my $hash = Nagios::HTTP::Util::gen_hash( cmd => $cmd, freq => $freq );

# Output
my $cronjob = "$min $hour * * * nagios $cmd | /usr/lib/nagios/plugins/nagios_http_result -n $name -h $hash\n";
if ($stdout) {
  print $cronjob;
  exit;
}

my $cronfile = "/etc/cron.d/nagios-http-$name";
my $cronfh = IO::File->new( $cronfile, 'w' ) 
  or die "Cannot write to '$cronfile': $!\n";
print $cronfh "\n$cronjob\n"
  or die "Print to '$cronfile' failed: $!\n";
$cronfh->close;

