#!/usr/bin/perl
#
# Helper script to setup a cron job running as nagios with the given command.
#
# Ideally we should be able to do this via some kind of user cron.d facility,
# but such a thing doesn't seem to exist, at least with linux vixie-cron.
#

use strict;
use File::Basename;
use Getopt::Long qw(:config no_ignore_case bundling);
use IO::File;
use YAML qw(Dump);

use Nagios::HTTP::Util;

my $max_freq = 12 * 60;
my $cronjob_path = '/usr/lib/nagios/plugins:/usr/lib64/nagios/plugins:/sbin:/usr/sbin:/bin:/usr/bin';

sub usage {
  print "$_[0]\n" if @_;
  die "usage: " . basename($0) . " [--freq <mins>] [--hour <hour>] [--minute <min>] [-u <url>] [-H <hostname>] -n <name> -C <cmd>\n";
}

my ($help, $cmd, @env, $freq, $hostname, $report_hostname, $name, $stdout, $verbose, $url);
my ($minute, $hour);
my $dow = '*';
usage
unless GetOptions(
  'help|h|?'        => \$help,
  'cmd|C=s'         => \$cmd,
  'env|E=s@'        => \@env,
  'name|n=s'        => \$name,
  'hostname|H=s'    => \$hostname,
  'report-hostname|report|R=s'    => \$report_hostname,
  'stdout'          => \$stdout,
  'url|u=s'         => \$url,
  'verbose|v+'      => \$verbose,
  'freq|f=i'        => \$freq,
  'minute|mi=s'     => \$minute,
  'hour|hh=s'       => \$hour,
  'dow|dayofweek=s' => \$dow,
);
usage if $help;
usage unless $cmd;
usage 'Cannot set both --hour and --freq > 60'   if $hour   && $freq && $freq >= 60;
usage 'Cannot set both --minute and --freq < 60' if $minute && $freq && $freq <  60;

# Untaint parameters
if ($name =~ m/^([-\w]+)$/) {
  $name = $1;
} else {
  die "Invalid characters in name '$name'";
}
if ($cmd =~ m! ^([-\w\s'",.;:@\#/\\\$=%&?&\+\*\(\){}<>\[\]|]+)$ !x) {
  $cmd = $1;
} else {
  die "Invalid characters in cmd '$cmd'";
}
if (@env) {
  my @untainted_env = ();
  for my $env (@env) {
    if ($env =~ m/^([-\w=]+)$/) {
      push @untainted_env, $1;
    } else {
      die "Invalid characters in env '$env'";
    }
  }
  @env = @untainted_env;
}
if ($hostname) {
  if ($hostname =~ m! ^([-\w]+)$ !x) {
    $hostname = $1;
  } else {
    die "Invalid characters in hostname '$hostname'";
  }
}
if ($report_hostname) {
  if ($report_hostname =~ m! ^([-\w]+)$ !x) {
    $report_hostname = $1;
  } else {
    die "Invalid characters in report_hostname '$report_hostname'";
  }
}
if ($freq) {
  if ($freq =~ m! ^([\d]+)$ !x) {
    $freq = $1;
  } else {
    die "Invalid characters in frequency '$freq'";
  }
}
if ($url) {
  if ($url =~ m! ^([-\w:/.?&;%,+@\#]+)$ !x) {
    $url = $1;
  } else {
    die "Invalid characters in url '$url'";
  }
}

# Generate hash from current settings
my $hash = Nagios::HTTP::Util::gen_hash(
  cmd       => $cmd,
  freq      => $freq,
  minute    => $minute,
  hour      => $hour,
  dow       => $dow,
  env       => \@env,
  $url ? ( url => $url ) : (),
  verbose => $verbose,
);

# Map frequency to cron style $minute $hour settings
$freq ||= 5;
my $cron = '';
if ($freq >= 60) {
  die "Error: frequency '$freq' is too large - max is $max_freq\n"
    if $freq > $max_freq;
  die "Error: frequency must be a factor of 60 if greater than 60\n"
    if $freq % 60;
  $minute ||= 45;
  $hour = '*/' . $freq / 60 if $freq > 60;
}
elsif ($freq > 30) {
  die "Error: frequency '$freq' is too large - max is 30 (or a multiple of 60)\n";
}
else {
  $minute ||= "*/$freq";
  $hour ||= '*';
}

# Output
$cmd =~ s/%/\\%/g;
my $cron_hostname = $report_hostname || $hostname;
my $cron_hostname_arg = $cron_hostname ? "-H $cron_hostname" : '';
my $url_arg = $url ? "--url $url " : '';
my $croncmd = "$cmd | /usr/lib/nagios/plugins/nagios_http_result -n $name -h $hash $url_arg $cron_hostname_arg";
my $cronjob = "$minute $hour * * $dow nagios $croncmd\n";
if ($stdout) {
  print $cronjob;
  exit;
}

# Setup cron file
umask 0022;
my $cronfile = "/etc/cron.d/nagios-http-$name" . ($report_hostname ? "-$report_hostname" : '');
my $cronfh = IO::File->new( $cronfile, 'w' )
  or die "Cannot write to '$cronfile': $!\n";
print $cronfh "PATH=$cronjob_path\n"
  or die "Print to '$cronfile' failed: $!\n";
if (@env) {
  print $cronfh "$_\n"
    or die "Print to '$cronfile' failed: $!\n"
      foreach @env;
}
print $cronfh "\n$cronjob\n"
  or die "Print to '$cronfile' failed: $!\n";
$cronfh->close;
chown 0, 0, $cronfile;

# Output status data
print Dump {
  status    => 'OK',
  hash      => $hash,
};

