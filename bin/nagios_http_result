#!/usr/bin/perl
#
# Helper script to capture the output of a cron job and post it somewhere.
#

use strict;
use File::Basename;
use Getopt::Long;
use Sys::Hostname;
use YAML qw(Dump);
use LWP::UserAgent;
use HTTP::Request;

sub usage { 
  print "usage: " . basename($0) . " [-v] [-u <url>] [--output <output>] -h <hash> -n <name>\n"; 
  print "  If no --output is supplied, reads from STDIN.\n";
  exit(1);
}

my $url = 'http://nagios/nagios-http';
my ($help, $hash, $name, $output, $stdout, $verbose);
usage
unless GetOptions(
  'help|?'          => \$help,
  'hash|h=s'        => \$hash,
  'name|n=s'        => \$name,
  'output|o=s'      => \$output,
  'stdout'          => \$stdout,
  'url|u=s'         => \$url,
  'verbose|v+'      => \$verbose,
);
usage if $help;
usage unless $hash && $name;

unless ($output) {
  local $/ = undef;
  $output = <>;
}
die "No output found\n" unless $output;

# Generate output
my $output = {
  hash => $hash,
  mtime => time,
  output => $output,
};

# Dump output if --stdout
if ($stdout) {
  print Dump $output;
  exit;
}

# Do a PUT of results back to $url/$hostname/$name
my $hostname = hostname;
$hostname =~ s/\..*//;

$url .= '/' unless substr($url,-1) eq '/';
$url .= "$hostname/$name";

my $ua = LWP::UserAgent->new;
my $req = HTTP::Request->new( PUT => $url );
$req->content_type('text/x-yaml');
$req->content(Dump $output);

my $res = $ua->request($req);

# Now only die if verbose: in general this just becomes cron noise,
# and the failure will get picked up anyway via the check_http_result 
# freshness check
die "PUT to url '$url' failed: " . $res->status_line 
  if $verbose && ! $res->is_success

