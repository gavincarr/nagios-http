#!/usr/bin/perl
#
# Helper script to capture the output of a cron job and post it somewhere.
#

use strict;
use File::Basename;
use Getopt::Long qw(:config no_ignore_case bundling);
use Sys::Hostname;
use Config::Tiny;
use YAML qw(Dump);
use LWP::UserAgent;
use HTTP::Request;

sub usage { 
  print "usage: " . basename($0) . " [-v] [-u <url>] [-H <hostname>] [--output <output>] -h <hash> -n <name>\n"; 
  print "  If no --output is supplied, reads from STDIN.\n";
  exit(1);
}

# Load config file
my $config = {};
my $config_file = '/etc/nagios_http_result.conf';
$config = Config::Tiny->read($config_file) if -f $config_file && -r $config_file;
if ($config->{env}) {
  $ENV{$_} = $config->{env}->{$_} foreach keys %{$config->{env}};
}

# Parse arguments
my ($help, $hash, $hostname, $name, $output, $stdout, $url, $verbose);
usage
unless GetOptions(
  'help|?'          => \$help,
  'hash|h=s'        => \$hash,
  'name|n=s'        => \$name,
  'output|o=s'      => \$output,
  'stdout'          => \$stdout,
  'hostname|H=s'    => \$hostname,
  'url|u=s'         => \$url,
  'verbose|v+'      => \$verbose,
);
usage if $help;
usage unless $hash && $name;
# The following args can also be set via an [args] section in the config file
if ($config->{args}) {
  $hostname ||= $config->{args}->{hostname};
  $url      ||= $config->{args}->{url} || 'http://nagios/nagios-http';
  $verbose  ||= $config->{args}->{verbose};
}

unless ($output) {
  local $/ = undef;
  $output = <>;
}
die "No output found\n" unless $output;

# Generate output
my $output = {
  hash => $hash,
  mtime => time,
  output => $output,
};

# Dump output if --stdout
if ($stdout) {
  print Dump $output;
  exit;
}

# Do a PUT of results back to $url/$hostname/$name
$hostname ||= hostname;
$hostname =~ s/\..*//;

$url .= '/' unless substr($url,-1) eq '/';
$url .= "$hostname/$name";

my $ua = LWP::UserAgent->new;
$ua->env_proxy;
my $req = HTTP::Request->new( PUT => $url );
$req->content_type('text/x-yaml');
$req->content(Dump $output);

my $res = $ua->request($req);

# Now only die if verbose: in general this just becomes cron noise,
# and the failure will get picked up anyway via the check_http_result 
# freshness check
if ($verbose) {
  if (! $res->is_success) {
    die "PUT to url '$url' failed: " . $res->status_line;
  }
  else {
    printf STDERR "PUT to url '$url' successful: %s\n", $res->status_line;
  }
}

